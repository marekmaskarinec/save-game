import (
  "rect.um"; "image.um"; "canvas.um"; "th.um"
  "d_util.um"; "d_global.um"; "d_resources.um"
)

var (
  imgClouds: image.Image
  imgRocks: image.Image
  width: real
)

type TileRepeat = struct {
  pos: th.Vf2
  w, h: uint
}

fn calculateTileRepeatOnScreen(tileSize, position: th.Vf2, screen: rect.Rect): TileRepeat {
  return TileRepeat{
    w: ceil(screen.w/tileSize.x+1), // Add one for tiles drawn in middle of scroll
    h: ceil(screen.h/tileSize.y+1),
    pos: th.Vf2{-d_util.fmod(screen.x+position.x, tileSize.x), -d_util.fmod(screen.y+position.y, tileSize.y)}}
}

fn init*(rl: ^d_resources.ResourceLoader) {
  imgClouds = d_resources.loadImage(rl, "data/bg00_sky.png")
  imgRocks = d_resources.loadImage(rl, "data/bg01_pit.png")
  imgRocks.setfilter(0)

  width = imgClouds.getDims().x
}

fn drawCloudStrip(scroll: real, y: real, colorMod: uint32) {
  scroll = d_util.fmod(scroll, -width)

  times := trunc(d_global.screen.w/width)+2

  for i := 0; i < times; i++ {
    imgClouds.draw(d_util.posTrans(scroll+i*width, y), colorMod)
  }
}

fn drawClouds*(screen: rect.Rect) {
  const (
    step = 60
    stripeCount = 5
  )

  origin := d_global.screen.h - 480 - (stripeCount - 1) * step + 120

  width = imgClouds.getDims().x

  canvas.drawRect(0x001133FF, screen) 

  for i := 0; i < stripeCount; i++ {
    yOffset := origin+i*step
    scroll := (i + 1) * (i + 1) * th.time/1000.0 * -120/stripeCount
    color := 0x7777FF00 | (trunc(((i + 1.0) / stripeCount) * 0xFF) & 0xFF)

    drawCloudStrip(scroll, yOffset, color);
  }
}

fn drawImageScaled(img: image.Image, pos: th.Vf2, scale: th.Vf2) {
  imgRocks.draw(th.Transform{p: th.Vf2{pos.x*scale.x, pos.y*scale.y}, s: scale})
} 

fn drawCave*(screen: rect.Rect, position: th.Vf2, scale: th.Vf2) {
  tileSize := imgRocks.getDims()
  scr := rect.mk(position.x, position.y, screen.w/scale.x, screen.h/scale.y)
  tileRepeat := calculateTileRepeatOnScreen(tileSize, th.Vf2{0, 0}, scr)

  for x := 0; x < tileRepeat.w; x++ {
    for y := 0; y < tileRepeat.h; y++ {
      imgRocks.draw(th.Transform{p: th.Vf2{x*tileSize.x, y*tileSize.y}.add(tileRepeat.pos).mul(scale).add(screen.getPos()), s: scale})
    }
  }
}

fn draw*(screen: rect.Rect) {
  drawCave(d_global.screen, th.Vf2{screen.x, screen.y}, th.Vf2{d_global.screen.w/screen.w, d_global.screen.h/screen.h})
}
